TITLE:: Notator
summary:: musicXML Writer with the rewrite feature of the given sclang code to play it via scserver
categories:: Notator
related:: Reference/ScoreGuideline, Classes/PitchClassSet, Classes/SPN

DESCRIPTION::
Using the link::Classes/Notator:: class, you can play the score via the sc-server and create musicXML files to seamlessly edit the score created in sclang in your favourite music notation software that supports musicXML 4.0 (or higher) import. It provides the following automated functions:
numberedlist::
##export a musicXML file and automatically open it in a music notation program,
##create an SCD file and automatically load and open it to play music:
list::
##all standard and abbreviated notation is rewritten;
##adds a new code block to play the transcribed code;
##tied notes and articulation are applied for playback;
##tries to associate the staff label with the same name as code::SynthDef::. code::Synth(\default):: will be used if the same defname is not found with the staff label.
::
::

You can print (or make a PDF of) the musicXML file without editing it, but you should edit and modify the score, since the link::Classes/Notator:: class is not intended for engraving music, but for composing seamlessly from music designed with SuperCollider.

note::
The musical notation for the link::Classes/Notator:: class can be opened as a form of score in any music software program that supports musicXML 4.0 (or higher) import and a score view functionality.
The scores used in the examples in this help document are encoded by Dorico 4 Pro for some examples that do not display the musicXML file correctly, but are otherwise encoded by MuseScore 4.
::

warning::
The way musicXML is decoded varies from software to software, so a musicXML file may look different in different software that opens it. Also, if the decoding is not correct, the score may look incorrect. You should therefore be familiar with the musicXML import characteristics of the software you use.
::

This section covers only the basic usage of the link::Classes/Notator::.
For details, refer to link::Reference/ScoreGuideline::. For simpler and eleganter examples using algorithmic code construction, refer to the following examples in link::#examples#Example:: section:

list::
##link::#Eighth-tone Scale through algorithmic construction::
##link::#Twelve-tone matrix through algorithmic construction::
::


subsection::Basic use

The musical notation for the link::Classes/Notator:: class is constructed as an array instance, each element of which represents the contents of a particular bar. The first element of the array instance, i.e. index 0 of the array instance, is assigned information about the title, composer and copyright of the piece, and the index of the first and subsequent bar contents starts from index 1. The voice number can vary from bar to bar, but the voice number of joined notes (or joined chords) by a tie across the bar line should be the same voice. The bar number and the index of the array instance are therefore identical.
The basic usage of link::Classes/Notator:: is as follows:

table::

##
Standard

Notation

soft:: ::

soft:: ::

(Array-

in-

Array

Notation)

||
code::
(
var title, score;

title = ''; // title of the piece.
// The value for the variable title should start with a lowercase character and
// cannot be an uppercase letter or number.

score = [
	(  title: \x, composer: \x, rights: '©'  ),
	// –––––––––  ––––––––––––  –––––––––––
	// |          |             |
	// title      composer      copyrights
	//
	//             staff lable: If the staff label is empty or nil, then piano will be used.
	//             |
	//             |                 attribute: key signature, time signature, number of staves,
	//             |                 |          clefs and optional transposition information.
	//             |                 |          The number of staves and the transposition
	//             |                 |          information defined in the first bar are fixed
	//             |                 |          to the end.
	//             |                 |          If the attribute is not defined, 4 staves are
	//             |                 |          used to easily cover the range of the piano.
	//             ––––––––––––––––  ––––––––––
	(  bar: 1, p1:(lbl: \staffLabel, atr: (...), v1:[...], v2: [...], ...), p2:(...), ...  ),
	// ––––––  –––––––––––––––––––––––––––––––––––––––––––––––––––––––––––– ––––––––  –––
	// |       |                                                            |         |
	// |       contents for player 1                                        |         and so on
	// |                                                                    |
	// bar number                                                           contents for player 2
	//
	(  bar: 2, p1:(atr: (...), v1: [...], v2: [...], ...), p2:(...), ...  ),
	//             ––––––––––  –––––––––  –––––––––  –––
	//             |           |          |          |
	//             |           |          |          and so on
	//             |           |          |
	//             |           |          v2: the entries of voice 2
	//             |           |
	//             |           v1: the entries of voice 1
	//             |
	//             accepted attributes from the second bar: key signature, time signature, clef(s)
	//
	...
];

x = Notator.notate(score, ("~/Downloads/" ++ title ++ ".musicXML").standardizePath, 'Dorico 4')
//                 –––––   –––––––––––––     –––––    ––––––––––– ––––––––––––––––  ––––––––––
//                 |       |                 |        |           |                 |
//                 |       |                 |        |           |                 the application
//                 |       |                 |        |           |                 that will open
//                 |       |                 |        |           |                 the exported
//                 |       |                 |        |           |                 musicXML file
//                 |       |                 |        |           |
//                 |       |                 |        |         standardise the path written in
//                 |       |                 |        |         a folder under the user's home folder
//                 |       |                 |        |           (starting with the ~ symbol).
//                 |       |                 |        |
//                 |       |                 |        the extension for the musicXML file
//                 |       |                 |
//                 |       |                 title of the piece above
//                 |       |
//                 |       the folder where the musicXML file will be saved
//                 |
//                 score constructed above
)
::
##
Syntactic

Sugar
||
code::
(
var title = \x;
x = [
	(  title: \x, composer: \x, rights: '©'  ),
	(  bar: 1, p1:(lbl: \staffLabel, atr: (...), v1:[...], v2: [...], ...), p2:(...), ...  ),
	(  bar: 2, p1:(lbl: \staffLabel, atr: (...), v1: [...], v2: [...], ...), p2:(...), ...  ),
	...
]
.notate(("~/Downloads/" ++ title ++ ".musicXML").standardizePath, 'Dorico 4')
)
::
##
Access

to

results
||
list::
##To play the music again via the scserver without having to recreate the music XML file:

code::
~play.()
::

##To open the musicXML file using the system's default application:

code::
x[0].openOS
::

##To reload the SCD file in SuperCollider:
code::
x[1].load
::

##To reopen the SCD file into SuperCollider:
code::
x[1].openDocument
::

##To open the folder containing the musicXML and SCD file using File Explorer (or Finder):

code::
x[0].dirname.openOS // x[1].dirname.openOS
::
::
::

There are three ways of notating musical information for link::Classes/Notator::, i.e. score construction using:

numberedlist::
##Array-in-Array Notation:

an array for each musical entry.

##Symbol-in-Array Notation:

a string or symbol for each musical entry.

##String Notation:

a string for the musical entry of a voice.
::

There would be many possibilties of playing the score according to the characteristic of music and the work style of users. In this help document, the simplest way which provided by the link::Classes/Notator:: class is provided.




subsection::Example

table::

##In this examples, Dorico 4 is used to open musicXML since MuseScore does not correctly show nested tuplets in the bar 2.

table::

##The

desired

score
||
table::
##image::resources/compact example.svg::
::
##Array

soft:: ::

per

soft:: ::

Entry

soft:: ::

soft:: ::

(Array-

in-

Array

Notation)
||

code::
(
var title = \ex_1, score;
score = [
	(
		title: title, composer: 'me', rights: '©'
	),
	(
		bar: 1,
		p1: (
			lbl: \test,
			atr: (key: [-3, \minor], time: [4, 4], staves: 1, clef: [[\g, 2]]),
			//                                    –––––––––
			//                                    |
			//                                    The number of staves defined in the first
			//                                    bar is fixed until the end.
			//
			v1:
			[[\t,\q,90],[\F],[\e5],[[\d,\f],$~],[$|,\x,$`],[[72,\ef]],[[71,74],\e,\sf],[\r,\q]],
			//–– –––––   ––   –––   |–––––  ––   –– –– ––    ––––––     –––––      –––  ––
			// |  |       |    |    | |  |  |    |  |  |     |   |      |          |    |
			// |  |       |    |    | |  |  tie  |  |  |     |   |      |          |    rest
			// |  |       |    |    | |  | start |  |  |     |   |      |          |
			// |  |       |    |    | |  |       |  |  |     |   |      |          sforzando
			// |  |       |    |    | |  |       |  |  |     |   |      |
			// |  |       |    |    | |  |       |  |  |     |   |      pitches for chord
			// |  |       |    |    | |  |       |  |  |     |   |
			// |  |       |    |    | |  |       |  |  |     MIDI pitch number and pitch name
			// |  |       |    |    | |  |       |  |  |     for chord with the previous
			// |  |       |    |    | |  |       |  |  |     rhythmic value
			// |  |       |    |    | |  |       |  |  |
			// |  |       |    |    | |  |       |  |  tie end
			// |  |       |    |    | |  |       |  |
			// |  |       |    |    | |  |       |  rhythmic value
			// |  |       |    |    | |  |       |  - \x: sixteenth note
			// |  |       |    |    | |  |       |  - \e: eighth note
			// |  |       |    |    | |  |       |  - \q: quarter note
			// |  |       |    |    | |  |       |  - \h: half note
			// |  |       |    |    | |  |       |
			// |  |       |    |    | |  |       the same pitch(es)
			// |  |       |    |    | |  |       as the pitch(es) of the previous entry
			// |  |       |    |    | |  |
			// |  |       |    |    | pitches for chord:
			// |  |       |    |    | the octave of previous pitch will be used.
			// |  |       |    |    | If the previous one is a chord,
			// |  |       |    |    | the octave of the last notated element in its array
			// |  |       |    |    | will be used.
			// |  |       |    |    |
			// |  |       |    |    [ ]: chord
			// |  |       |    |
			// |  |       |    pitch (scientific pitch notation)
			// |  |       |    with default rhythmic value (quarter note)
			// |  |       |
			// |  |       \F: dynamic.
			// |  |       \f: pitch class.
			// |  |
			// |  90: bpm (quarter note)
			// |
			// \t: tempo
			//
			v2: [[\c4, \dq, $(], [\b3, \e], [60, $)], [[55, \d4], \sf], [\r], [\g, \pp, $!]]
			//         –––  ––                   ––                                –––  ––
			//         |    |                    |                                 |    |
			//         |    slur start           slur end                      dynamic  |
			//         dotted quarter note                                              |
			//         dotted quarter note                                         staccatissimo
		)
	),
	(
		bar: 2,
		p1: (
			v1: [[\r, \q], [\c, \h, \p], [\b4, \q]],
			v2: [
				//                         rhythmic value and tuplet unit are the same
				//                         when there are three elements for a tuplet
				//                         |
				//         staccato        |
				// tenuto      |    tuplet |  three in place of two    nested tuplet
				//   |         |         | |  |                        |
				//   ——        ––        – –– ––––                     ––
				[\c, $-], [\d, $.], [\e, [\e, 3, 2]], [\f, [\e]], [\g, [[\x, 3, 2]]],
				[\f, [[\x]]], [\e, [[\x, \], \]], [\d, \h]
				//                       ––  ––
				//                       |   |
				//                       |   the end of the tuplet
				//                       |
				//                       the end of the nested tuplet
			]
		)
	)
];

x = Notator.notate(
	score,
	("~/Downloads/" ++ title ++ ".musicXML").standardizePath,
	'Dorico 4'
)
)
::
##Symbol

(or string)

soft:: ::

per

soft:: ::

Entry

soft:: ::

soft:: ::

(Symbol-

in-

Array

Notation)
||

code::
(
var title = \ex_1, score;
score = [
	(
		title: title, composer: 'me', rights: '©'
	),
	(
		bar: 1,
		p1: (
			lbl: \test,
			atr: (key: [-3, \minor], time: [4, 4], staves: 1, clef: [[\g, 2]]),
			v1: ['t q 90', \F, \e5, '<d f> ~', '| 16 `', '<72 ef>', '<71 74> 8 sf', 'r 4'],
			//                       –––––        ––      –––––––    ––––––– –         –
			//                       |            |       |          |       |         |
			//                       chord        |      chord      chord    |   rhythmic value:
			//                                    |                          |   quarter note
			//                                    |                          |
			//                                    rhythmic value:      rhythmic value:
			//                                    sixteenth note       eighth note

			v2: ["c4 4. (", "b3 8", "60 )", "<55 d4> sf", \r, 'g pp !']
			//                                                   |
			//                                                   dynamic
		)
	),
	(
		bar: 2,
		p1: (
			v1: ["r 4", "c 2 p", "b4 4"],
			v2: [
				"c -", "d .", "e /8/3:2", "f /8", "g //16/3:2", 'f //16', 'e //16//', 'd 2'
			]
		)
	)
];

x = Notator.notate(
	score,
	("~/Downloads/" ++ title ++ ".musicXML").standardizePath,
	'Dorico 4'
)
)
::
##String

soft:: ::

per

soft:: ::

Whole

Entries

per

Voice

soft:: ::

soft:: ::

(String Notation)
||
code::
(
var title = \ex_1, score;
score = [
	(
		title: title, composer: 'me', rights: '©'
	),
	(
		bar: 1,
		p1: (
			lbl: \test,
			atr: (key: [-3, \minor], time: [4, 4], staves: 1, clef: [[\g, 2]]),
			v1: "t_q_90 F e5 <d_f>_~ |_16_` <72_ef> <71_74>_8_sf r_4",
			v2: "c4_4._( b3_8 60_) <55_d4>_sf r g_pp_!"
		)
	),
	(
		bar: 2,
		p1: (
			v1: "r_q c_h_p b4_q",
			v2: "c_- d_. e_/8/3:2 f_/8 g_//16/3:2 f_//16 e_//16// d_2"
		)
	)
];

x = Notator.notate(
	score,
	("~/Downloads/" ++ title ++ ".musicXML").standardizePath,
	'Dorico 4'
)
)
::
##Automatic

rewrite of

the

sclang

code

to play

in SC
||
code::
(
~ex_1 = ( // score start
	'test': ( // part start
		1: ( // bar 1 start
			v1: [ // voice start
				(tempo: (beatUnit: \quarter, beatUnitNumDot: 0, bpm: 90)),
				(tempoNormalised: (beatUnit: \quarter, bpm: 90.0)),
				(dynamicForPart: \f),
				(
					dur: 1.0, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [76.0], pitch: ['e5'],
					dynamic: \f, velocity: 79
				),
				(
					dur: 1.0, tie: \start, slur: \nil, articulation: 'nil',
					midinote: [74.0, 77.0], pitch: ['d5', 'f5'],
					dynamic: \f, velocity: 79
				),
				(
					dur: 0.25, tie: \stop, slur: \nil, articulation: 'nil',
					midinote: [74.0, 77.0], pitch: ['d5', 'f5'],
					dynamic: \f, velocity: 79
				),
				(
					dur: 0.25, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [72.0, 75.0], pitch: ['c5', 'ef5'],
					dynamic: \f, velocity: 79
				),
				(
					dur: 0.5, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [71.0, 74.0], pitch: ['b4', 'd5'],
					dynamic: \sf, velocity: 96
				),
				(
					dur: 1.0, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: Rest(1.0), pitch: Rest(1.0),
					dynamic: \sf, velocity: 96
				)
			], // voice end
			v2: [ // voice start
				(
					dur: 1.5, tie: \nil, slur: \start, articulation: 'nil',
					midinote: [60.0], pitch: ['c4'],
					dynamic: \mp, velocity: 55
				),
				(
					dur: 0.5, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [59.0], pitch: ['b3'],
					dynamic: \mp, velocity: 55
				),
				(
					dur: 0.5, tie: \nil, slur: \stop, articulation: 'nil',
					midinote: [60.0], pitch: ['c4'],
					dynamic: \mp, velocity: 55
				),
				(
					dur: 0.5, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [55.0, 62.0], pitch: ['g3', 'd4'],
					dynamic: \sf, velocity: 96
				),
				(
					dur: 0.5, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: Rest(0.5), pitch: Rest(0.5),
					dynamic: \sf, velocity: 96
				),
				(
					dur: 0.5, tie: \nil, slur: \nil, articulation: 'staccatissimo',
					midinote: [67.0], pitch: ['g4'],
					dynamic: \pp, velocity: 31
				)
			] // voice end
		), // bar end
		2: ( // bar 2 start
			v1: [ // voice start
				(
					dur: 1.0, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: Rest(1.0), pitch: Rest(1.0),
					dynamic: \sf, velocity: 96
				),
				(
					dur: 2.0, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [72.0], pitch: ['c5'],
					dynamic: \p, velocity: 43
				),
				(
					dur: 1.0, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [71.0], pitch: ['b4'],
					dynamic: \p, velocity: 43
				)
			], // voice end
			v2: [ // voice start
				(
					dur: 0.5, tie: \nil, slur: \nil, articulation: 'tenuto',
					midinote: [60.0], pitch: ['c4'],
					dynamic: \pp, velocity: 31
				),
				(
					dur: 0.5, tie: \nil, slur: \nil, articulation: 'staccato',
					midinote: [62.0], pitch: ['d4'],
					dynamic: \pp, velocity: 31
				),
				(
					dur: 0.33333333333333, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [64.0], pitch: ['e4'],
					dynamic: \pp, velocity: 31
				),
				(
					dur: 0.33333333333333, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [65.0], pitch: ['f4'],
					dynamic: \pp, velocity: 31
				),
				(
					dur: 0.11111111111111, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [67.0], pitch: ['g4'],
					dynamic: \pp, velocity: 31
				),
				(
					dur: 0.11111111111111, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [65.0], pitch: ['f4'],
					dynamic: \pp, velocity: 31
				),
				(
					dur: 0.11111111111111, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [64.0], pitch: ['e4'],
					dynamic: \pp, velocity: 31
				),
				(
					dur: 2.0, tie: \nil, slur: \nil, articulation: 'nil',
					midinote: [62.0], pitch: ['d4'],
					dynamic: \pp, velocity: 31
				)
			] // voice end
		) // bar end
	) // part end
); // score end

~play = {
	s.waitForBoot {
		var parts, detectNumVoices, eventPlayer;
		parts = [ 'test' ];
		detectNumVoices = { |nthBar, thisPart|
			~ex_1[thisPart][nthBar].keys.count { |item|
				item.asString.contains("v") }
		};
		eventPlayer = { |thisEntry, velocityFactorForRestNTiedNote, slur, instrument|
			var articulationToVelocity, articulationToLegato, sustain;
			# articulationToVelocity, articulationToLegato = switch (thisEntry[\articulation],
				'accent',          { [4, 1]    },
				'strong-accent',   { [8, 1]    },
				'staccato',        { [1, 0.4]  },
				'staccatissimo',   { [2, 0.2] },
				'tenuto',          { [0, 1]    },
				'detached-legato', { [0, 0.8]  },
				'nil',             { [0, 1]    }
			);

			("\t- articulationToLegato:" + articulationToLegato).postln;
			articulationToLegato = if (slur == \on) {
				1
			} {
				if (thisEntry[\tie] == \lv) {
					1.2
				} { 0.9
				}
			} * articulationToLegato;
			("\t- slur-applied articulationToLegato:" + articulationToLegato).postln;
			(
				instrument: instrument,
				dur:        if (thisEntry[\tiedDur] == 0) {
					thisEntry[\dur]
				} {
					thisEntry[\tiedDur]
				},
				amp:        thisEntry[\velocity] / 127 * velocityFactorForRestNTiedNote,
				midinote:   thisEntry[\midinote],
				legato:     articulationToLegato
			).play.postln;
			"".postln;
		};

		parts.do { |aPart|
			var numBars, nthBar, instrument, scorePlay;
		numBars = ~ex_1[parts[0]].size;
			nthBar = 1;
			instrument = if (SynthDescLib.global.synthDescs.keys.includes(aPart)) {
				aPart
			} {
				\default
			};
			scorePlay = { |nthBarNum|
				var thisPartThisBarVoiceSize = detectNumVoices.(nthBarNum, aPart);
				("\nbar:" + nthBarNum).postln;
				thisPartThisBarVoiceSize.do { |thisVoiceIndex|
					thisVoiceIndex = thisVoiceIndex + 1;
					fork{
						var thisPartThisBarThisVoice, thisPartThisBarThisVoiceLastItemIndex, slur;
						thisPartThisBarThisVoice = ~ex_1[
							aPart][nthBarNum][
							("v" ++ thisVoiceIndex).asSymbol
						];
						thisPartThisBarThisVoiceLastItemIndex = thisPartThisBarThisVoice.size - 1;
						thisPartThisBarThisVoice.size.do { |thisVoiceItemIndex|
							var entryCurrent, entryNext, entryPrevious;

							(
							"\n\nthisVoiceItemIndex:"
								+ thisVoiceItemIndex + "@ bar" + nthBarNum ++ "\n"
							).postln;
							# entryPrevious, entryCurrent, entryNext = (
								case
								{ thisVoiceItemIndex < thisPartThisBarThisVoiceLastItemIndex
									&&  (thisVoiceItemIndex != 0) } {
									[
										thisPartThisBarThisVoice[thisVoiceItemIndex - 1],
										thisPartThisBarThisVoice[thisVoiceItemIndex],
										thisPartThisBarThisVoice[thisVoiceItemIndex + 1]
									]
								}
								{ thisVoiceItemIndex == thisPartThisBarThisVoiceLastItemIndex  &&  (thisVoiceItemIndex != 0)} {
									[
										thisPartThisBarThisVoice[thisVoiceItemIndex - 1],
										thisPartThisBarThisVoice[thisVoiceItemIndex],
										if (nthBarNum < numBars) {
											~ex_1[aPart][nthBarNum + 1][
												("v" ++ thisVoiceIndex).asSymbol
											][0]
										}
									]
								}
								{ thisVoiceItemIndex == 0 } {
									[
										if (nthBarNum == 1) {
											(tie: \nil)
										} {
											var previousItem = ~ex_1[aPart][nthBarNum - 1][
												("v" ++ thisVoiceIndex).asSymbol
											];
											previousItem[previousItem.size - 1]
										},
										thisPartThisBarThisVoice[thisVoiceItemIndex],
										thisPartThisBarThisVoice[thisVoiceItemIndex + 1]
									]
								}
							);

							(
								"\n\t* thisVoiceItemIndex:" + thisVoiceItemIndex ++ "\n\n\t*"
								+ entryCurrent ++ "\n"
							).postln;

							case
							{ entryCurrent.keys.includes(\tempo) } {
								TempoClock.default.tempo = entryCurrent[\tempo][\bpm] / 60;
								(
									"\t* TempoClock.default.tempo:"
									+ TempoClock.default.tempo ++ "\n"
								).postln
							}
							{ thisPartThisBarThisVoice[thisVoiceItemIndex].keys.includes(\midinote)
							} {
								var tiedRhythmicValue = 0, velocityFactor = 1, waitTime;
								switch (entryCurrent[\slur],
									\start, { slur = \on },
									\stop, { slur = \off }
								);
								if (entryCurrent[\tie] == \start
									&&  "^start".matchRegexp(entryPrevious[\tie].asString).not) {
									var actualiseRhythmicValue;
									actualiseRhythmicValue = { |testBar, testItemIdx, testItem|
										var nextTestItemDetector, nextTestItem, nexttestItemIdx;
											nextTestItemDetector = { |testBarNum|
											~ex_1[aPart][testBarNum][
												( "v" ++ thisVoiceIndex).asSymbol
												]
											};

										"\n\t* Checking the next tie status".postln;
										(
											"\n\t\t* nthBar:" + testBar ++
											"; testItemIdx:" + testItemIdx ++
											"; testItem[\dur]:" + testItem[\dur]
											).postln;
										(
											"\n\t\t* previous tiedRhythmicValue:"
											+ tiedRhythmicValue
										).postln;

										tiedRhythmicValue = tiedRhythmicValue + testItem[\dur];

										("\t\t* updated tiedRhythmicValue:" + tiedRhythmicValue).postln;

										if (testItemIdx < (nextTestItemDetector.(testBar).size - 1)) {
											nexttestItemIdx = testItemIdx + 1;
											nextTestItem = nextTestItemDetector.(testBar)[nexttestItemIdx];
										} {
											testBar = testBar + 1;
											nexttestItemIdx = 0;
											nextTestItem = nextTestItemDetector.(testBar)[nexttestItemIdx];
										};

										("\t\t* the nextTestItem['tie'] of the testItem" + testItemIdx ++ ":" + nextTestItem[\tie]).postln;
										("\t\t*" + "^stop".quote ++ ".matchRegexp(nextTestItem['tie'].asString): " + "^stop".matchRegexp(nextTestItem[\tie].asString)).postln;
										if ("^stop".matchRegexp(testItem[\tie].asString)) {
											"\t\t* no need to check the further ties\n".postln
										} {
											"\n\t* Checking the next tie status";
											actualiseRhythmicValue.(testBar, nexttestItemIdx, nextTestItem);
										}
									};

									actualiseRhythmicValue.(nthBarNum, thisVoiceItemIndex, entryCurrent);
								};

								("\t* entryPrevious 'waitTimeCorrectionForTiedNote':"+ entryPrevious[\waitTimeCorrectionForTiedNote] ++ "\n").postln;
								if ("^start".matchRegexp(entryPrevious[\tie].asString)) {
									entryCurrent.put(\waitTimeCorrectionForTiedNote, 0);
									velocityFactor = 0
								};

								if ("^Rest".matchRegexp(entryCurrent[\midinote].asString)) {
									velocityFactor = 0
								};

								entryCurrent.put(\tiedDur, tiedRhythmicValue);
								(
									"\t* entryCurrent 'tiedDur':" + entryCurrent['tiedDur'] ++ ";"
									"\t'dur':" + entryCurrent['dur'] ++ ";"
									"\t'waitTimeCorrectionForTiedNote':" + entryCurrent['waitTimeCorrectionForTiedNote'] ++ ";\n"
								).postln;
								eventPlayer.(entryCurrent, velocityFactor, slur, instrument);
								waitTime = if (entryCurrent[\waitTimeCorrectionForTiedNote] != nil) {
									entryCurrent[\waitTimeCorrectionForTiedNote]
								} {
									if (entryCurrent[\tiedDur] == 0) {
										entryCurrent[\dur]
									} {
										entryCurrent[\tiedDur]
									}
								};
								("	* v" ++ thisVoiceIndex + "waitTime:" + waitTime).postln;
								waitTime.wait
							};
							("	*" + (thisVoiceItemIndex) + "/" + (thisPartThisBarThisVoiceLastItemIndex) + "finished").postln;
							("	* nth bar of total bars:" + nthBar + "/" + numBars).postln;
							if (thisVoiceItemIndex == thisPartThisBarThisVoiceLastItemIndex) {
								if (nthBar < numBars) {
									nthBar = nthBar + 1;
									scorePlay.(nthBar)
								}
							}
						}
					}
				}
			};
			scorePlay.(nthBar)
		}
	}
};
~play.()
)
::
::
::

CLASSMETHODS::

METHOD:: notate
argument:: score
A unique construction of musical notation information in the form of arrays and events.
argument:: musicXMLfilePath
Path for musicXML file
argument:: app
Application that is launched when a musicXML file is saved.


INSTANCEMETHODS::


EXAMPLES::


subsection::Eighth-tone Scale through algorithmic construction


table::
##
The

desired

score
||
image::resources\Eighth-tone_Scale.svg::
##
Array

soft:: ::

per

soft:: ::

Entry

soft:: ::

soft:: ::

(Array-

in-

Array

Notation)
||
code::
(
var title = "Eighth-tone Scale", score;
score = [
	(
		title: title, composer: 'a composer', rights: '©'
	),
	(
		bar: 1, p1: (
			lbl: \Violin,
			atr: (key: [0, \none], time: [4, 4], staves: 1, clef: [[\g, 2]]),
			v1: []
		)
	)
];

(0, 1/4 .. 12).mirror.clump(4).do { |notesInBar, index|
	var bar = index + 1;
	if (bar > 1) { score = score.add((bar: bar, p1: (v1: []))) };
	notesInBar.collect { |aNote|
		score[bar][\p1][\v1] = score[bar][\p1][\v1].add([aNote + 60]) }
};

x = Notator.notate(
	score,
	("~/Downloads/" ++ title ++ ".musicXML").standardizePath,
	'MuseScore 4'
)
)
::
::





subsection::Twelve-tone matrix through algorithmic construction

table::
##
A

possible

score
||
image::resources/twelve-tone_seriesnotation.svg::
##Array

soft:: ::

per

soft:: ::

Entry

soft:: ::

soft:: ::

(Array-

in-

Array

Notation)
||
code::
(
var title = "twelve-tone series", matrix_12tone;
matrix_12tone = { |array|
	var matrix = 12.collect { |i| array[i]; array - (array[i] % 12) };
	matrix.do { |item| item.replace(11, \e).replace(10, \t).postln }
};
matrix_12tone = matrix_12tone.((0..11).scramble) + 72;

(
x = [(title: '12-tone series matrix', composer: 'randomised', rights: '©')] ++
matrix_12tone.collect { |series, index|
	if (index == 0) {
		(
			bar: index + 1,
			p1: (
				lbl: '',
				atr: (key: [0, \none], time: \x, staves: 1, clef: [[\g, 2]]),
				v1: series.collect { |aNote| [aNote, \w] }
			)
		)
	} {
		series.postln;
		(
			bar: index + 1,
			p1: (v1: series.collect { |aNote| [aNote, \w] })
		)
	}
}
).notate(("~/Downloads/" ++ title ++ ".musicXML").standardizePath, 'MuseScore 4')
)
::
::


subsection::Default staves and default instrument (piano)

table::

##The

desired

score
||
In this examples, Dorico 4 is used to open musicXML since MuseScore does not correctly show nested tuplets staves.
table::
##image::resources/default staves.svg::
::
##Array

soft:: ::

per

soft:: ::

Entry

soft:: ::

soft:: ::

(Array-

in-

Array

Notation)
||
code::
(
var title = 'default staves', score;
score = [
	(
		title: title, composer: 'me', rights: '©'
	),
	(
		bar: 1,
		p1: (
			v1: [
				[\t, \q, 120],
				[[\a0, \a3, \a5, \c8], [\e, 3, 2], \p, \a, \u],
				[$|, [[\x, 3, 2]], \f, \s],
				[$|, [[\e, \]], \f, \S],
				[$|, [5, \], \j],
				[$|, [\De, 6, 4, \x], \J],
				[$|, [\t], \sf, \O],
				[$|, [\x], \sf, \o],
				[$|, [4, \], \sf, \A],
				[$|, [\de, 5, 4, \x]],
				[\aqs4, [\e, \], \sf, \j],
				[$|, [\de, 3, 2, \e], \J],
				[$|, [\t, \], \s2, \j]
			]
		)
	),
	(
		bar: 2,
		p1: (
			v1: [
				[$|, [\e, 3, 2], \J],
				[\s1],
				[$|, [\e]],
				[$|, [5, \]],
				[\a4, [\x, 5, 4]],
				[$|, [\x]],
				[$\\],
				[$|, [[\x, 3, 2]]],
				[$|, [[\x]]],
				[$|, [[\x, \], \]],
				[$|, \dx],
				[\a2, \t],
				[$|, \Dx],
				[$|, \i],
				[$|, \Te],
				[$|, \i, \U],
			]
		)
	)
];

x = Notator.notate(
	score,
	("~/Downloads/" ++ title ++ ".musicXML").standardizePath,
	'Dorico 4'
)
)
::
##Symbol

(or string)

soft:: ::

per

soft:: ::

Entry

soft:: ::

soft:: ::

(Symbol-

in-

Array

Notation)
||
code::
(
var title = 'default staves', score;
score = [
	(
		title: title, composer: 'me', rights: '©'
	),
	(
		bar: 1,
		p1: (
			v1: [
				"t q 120",
				"<a0 a3 a5 c8> /8/3:2 p > (",
				"| //16/3:2 f .",
				"| //8/ f !",
				"| /8/ ~",
				"| /8../6:4/x `",
				"| /32 sf _",
				"| /16 sf -",
				"| /16/ sf ^",
				"| /8./5:4/x",
				"aqs4 /e/ sf ~",
				"| /8./3:2/e `",
				"| /32/ ff ~"
			]
		)
	),
	(
		bar: 2,
		p1: (
			v1: [
				'aqs4 /8/3/2 `',
				\s1,
				'| /8',
				'| /8/',
				'a4 /16/5/4',
				'| /16',
				'\\',
				'| //16/3/2',
				'| //16',
				'| //16//',
				'| 16.',
				'a2 32',
				'| 16..',
				'| 64',
				'| 8...',
				'| 64 )'
			]
		)
	)
];

x = Notator.notate(
	score,
	("~/Downloads/" ++ title ++ ".musicXML").standardizePath,
	'Dorico 4'
)
)
::
##String

soft:: ::

per

soft:: ::

Whole

Entries

per

Voice

soft:: ::

soft:: ::

(String Notation)
||
code::
(
var title = 'default staves', score;
score = [
	(
		title: title, composer: 'me', rights: '©'
	),
	(
		bar: 1,
		p1: (
			v1: "
t_q_120
<a0_a3_a5_c8>_/e/3:2_p_>_(
|_//16/3:2_f_.
|_//8/_f_!
|_/8/_~
|_/8../6:4/16_`
|_/32_sf__
|_/16_sf_-
|_/16/_sf_^
|_/8./5:4/16
aqs4_/8/_sf_~
|_/8./3:2/8_`
|_/32/_ff_~
"
		)
	),
	(
		bar: 2,
		p1: (
			v1: "
aqs4_/8/3:2_`
s1
|_/8 |_/8/ a4_/16/5:4 |_/16 \\ |_//16/3:2 |_//16 |_//16//
|_16. a2_32 |_16.. |_64 |_8... |_64_)
"
		)
	)
];

x = Notator.notate(
	score,
	("~/Downloads/" ++ title ++ ".musicXML").standardizePath,
	'Dorico 4'
)
)
::
::










subsection::Nested tuplets and multiple voices





table::
##The

desired

score
||
table::
##
In this examples, Dorico 4 is used to open musicXML since MuseScore does not correctly show nested tuplets.

image::resources/Nested_tuplets_and_multiple_voices dorico 4.svg::
::
##Array

soft:: ::

per

soft:: ::

Entry

soft:: ::

soft:: ::

(Array-

in-

Array

Notation)
||
code::
(
var title = 'Nested tuplets and multiple voices';
x = [
	(title: title, composer: 'randomised', rights: '©'),
	(
		bar: 1,
		p1: (
			v1: [
				[[\a7, \a6], [\e, 3, 2, \e]],
				[$|, [\e]],
				[$|, [5, \]],
				[[\af7, \as6], [\e, 3, 2, \e]],
				[$|, [[\x, 3, 2, \x]]],
				[$|, [[\x]]],
				[$|, [[\x, \]]],
				[$|, [\e, \]],
				[$|, [\e, 5, 4, \e]],
				[$|, [[\e, 3, 2, \e]]],
				[$|, [[\q, \]]],
				[$|, [\q, \]]
			],
			v2: [
				[[\a5, \a4], [\x, 5, 4, \x]],
				[$|, [\x]],
				[$|, [4]],
				[$|, [\x]],
				[$|, [4, \]],
				[[\af5, \a4], [\x, 6, 4, \x]],
				[$|, [\x]],
				[$|, [5]],
				[$|, [\x]],
				[$|, [4, \]]
			],
			v3:
			[
				[[\a3, \a2], 6]
			],
			v4: [
				[[\a1, \a0]]
			]
		)
	)
].notate(("~/Downloads/" ++ title ++ ".musicXML").standardizePath, 'Dorico 4')
)
::
##Symbol

(or string)

soft:: ::

per

soft:: ::

Entry

soft:: ::

soft:: ::

(Symbol-

in-

Array

Notation)
||

code::
(
var title = 'Nested tuplets and multiple voices';
x = [
	(title: title, composer: 'randomised', rights: '©'),
	(
		bar: 1,
		p1: (
			v1: [
				'<a7 a6> /8/3:2', '| /8', '| /8/',
				'<af7 as6> /8/3:2', '| //16/3:2', '| //16', '| //16/', '| /8/',
				'| /8/5:4', '| //8/3:2', '| //4/', '| /4/'
			],
			v2: [
				'<a5 a4> /16/5:4', '| /16', '| /16', '| /16', '| /16/',
				'<af5 a4> /16/6:4', '| /16', '| /8', '| /16', '| /16/'
			],
			v3: ['<a3 a2> 4'],
			v4: ['<a1 a0>']
		)
	)
].notate(("~/Downloads/" ++ title ++ ".musicXML").standardizePath, 'Dorico 4')
)
::
##String

soft:: ::

per

soft:: ::

Whole

Entries

per

Voice

soft:: ::

soft:: ::

(String Notation)
||
code::
(
var title = 'Nested tuplets and multiple voices';
x = [
	(title: title, composer: 'randomised', rights: '©'),
	(
		bar: 1,
		p1: (
			v1: "
<a7_a6>_/8/3:2 |_/8 |_/8/
<af7_as6>_/8/3:2 |_//16/3:2 |_//16 |_//16/ |_/8/ |_/8/5:4 |_//8/3:2 |_//4/ |_/4/",
			v2: "
<a5_a4>_/16/5:4 |_/16 |_/16 |_/16 |_/16/ <af5_a4>_/16/6:4 |_/16 |_/8 |_/16 |_/16/",
			v3: "<a3_a2>_4",
			v4: "<a1_a0>"
		)
	)
].notate(("~/Downloads/" ++ title ++ ".musicXML").standardizePath, 'Dorico 4')
)
::
::